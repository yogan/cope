#!/usr/bin/env perl
use App::Cope;
use App::Cope::Extra qw[user %permissions %filetypes];

my $date_re = qr{
                  (?:\d{4}-)?     # year
                  \d{2} - \d{2}   # month-day
                  \s \d{2}:\d{2}  # hour-minute
                  (?::\d{2}.\d+\s # iso time thing
                    \+\d{4})?
                  \b              # end on a boundary
              }x;

sub size {
  # only files have actual file sizes
  if ( /^-/ ) {
    return 'green bold';
  } else {
    return 'green';
  }
}

my $highlighting = 0;

sub process {
  # Check whether we have to highlight at all
  if (/^total \d/) {
    $highlighting = 1;
    mark qr{^.+} => 'underline';
  }

  if ($highlighting) {
    my $ts = join '', keys %filetypes;
    line qr{([-$ts])(.)(.)(.)(.)(.)(.)(.)(.)(.)\s+(\d+)\s+(\S+)\s*(\S*)\s+([0-9.]+)([KMG]?)\s} =>
      \%filetypes, ( \%permissions ) x 9, 'red',
      \&{ user 'yellow' }, 'yellow', \&size, 'green';

    mark $date_re => 'green';
  }
}

run( \&process, real_path(), @ARGV );
