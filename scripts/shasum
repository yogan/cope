#!/usr/bin/env perl
use App::Cope;

# md5sum and its ilk output hashes. cache the hashes and highlight in
# bold if any of them matches another one.

my @colours = qw[green blue red yellow cyan magenta];
my %hashes;
my $i = 0;

sub process {
  if (/^([0-9a-f]{40})\s/) {
    if ( $1 eq "da39a3ee5e6b4b0d3255bfef95601890afd80709" ) { # shasum("")
      return;  # ignore empty files
    }
    elsif ( $hashes{$1} ) {
      mark qr{^\S+} => "bold " . $hashes{$1};
    }
    else {
      $hashes{$1} = $colours[ $i++ % 6 ];
      mark qr{^\S+} => $hashes{$1};
    }
  }

  # checking output
  else {
    line qr{: (OK|FAILED)$} => { OK => 'green bold', FAILED => 'red bold' };
    line qr{shasum: (WARNING:) (\d+) (.+)} => 'white on_red', 'red bold', 'red';
  }

}

run( \&process, real_path, @ARGV );
